#version 330 core
precision highp float;

#extension EXT_gpu_shader4 : require

layout( location = 0 )  in vec4 position;           //xyz pos, w speed
layout( location = 1 )  in vec4 prev_position;      //xyz prevPos, w life
layout( location = 2 )  in vec4 direction;			//xyz direction, w 0
 
uniform mat4 MVP; 
 
uniform float time;
   
//constants
const float PI = 3.14159;
const float TWO_PI = 2*PI;
const float PI_BY_2 = PI*0.5;
const float PI_BY_4 = PI_BY_2*0.5;
 
//shader outputs
out vec4 out_position;
out	vec4 out_prev_position;
out vec4 out_direction;

const float DAMPING_COEFFICIENT =  0.9995;
const vec3 emitterForce = vec3(0.0f,-0.001f, 0.0f);
const vec4 collidor = vec4(0,1,0,0);
const vec3 emitterPos = vec3(0);

float emitterYaw = (0.0f);
float emitterYawVar	= TWO_PI;
float emitterPitch	= PI_BY_2;
float emitterPitchVar = PI_BY_4;
float emitterSpeed = 0.05f;
float emitterSpeedVar = 0.01f;
 
int	  emitterLife = 60;
int   emitterLifeVar = 15;
 
const float UINT_MAX = 4294967295.0;

uint randhash(uint seed)
{
    uint i=(seed^12345391u)*2654435769u;
    i^=(i<<6u)^(i>>26u);
    i*=2654435769u;
    i+=(i<<5u)^(i>>12u);
    return i;
}

float randhashf(uint seed, float b)
{
    return float(b * randhash(seed)) / UINT_MAX;
}

void RotationToDirection(float pitch, float yaw, out vec3 direction)
{
	direction.x = -sin(yaw) * cos(pitch);
	direction.y = sin(pitch);
	direction.z = cos(pitch) * cos(yaw);
}

 
void main(void) 
{    
	vec3 prevPos = prev_position.xyz;
	int life = int(prev_position.w);
	vec3 pos  = position.xyz;
	float speed = position.w;
	vec3 dir = direction.xyz; 

	if(life > 0) {
		prevPos = pos;
		pos += dir*speed; 
		if(dot(pos+emitterPos, collidor.xyz)+ collidor.w <0) {			 
			dir = reflect(dir, collidor.xyz);
			speed *= DAMPING_COEFFICIENT;
		}  
		dir += emitterForce;
		life--;
	}  else {
		uint seed =   uint(time + gl_VertexID); 
		life = emitterLife + int(randhashf(seed++, emitterLifeVar));    
        float yaw = emitterYaw + (randhashf(seed++, emitterYawVar ));
		float pitch = emitterPitch + randhashf(seed++, emitterPitchVar);
		RotationToDirection(pitch, yaw, dir);		 
		float nspeed = emitterSpeed + (randhashf(seed++, emitterSpeedVar ));
		dir *= nspeed;  
		pos = emitterPos;
		prevPos = emitterPos; 
		speed = 1;
	}  
	out_position = vec4(pos, speed);	
	out_prev_position = vec4(prevPos, life);		
	out_direction = vec4(dir, 0);
	gl_Position = MVP*vec4(pos, 1);
 
}